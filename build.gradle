plugins {
    id 'java'
    id 'application'
    id 'jacoco'
}

group = 'com.mockgen'
version = '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-csv:1.10.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
    testImplementation 'org.mockito:mockito-core:5.5.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.5.0'
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

application {
    mainClass = 'com.mockgen.Main'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    violationRules {
        rule {
            element = 'PACKAGE'
            includes = ['com.mockgen.service', 'com.mockgen.dao']
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.50
            }
        }

        rule {
            element = 'PACKAGE'
            includes = ['com.mockgen.config']
            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                minimum = 0.0  // Нет требований
            }
        }
    }
}

check.dependsOn jacocoTestCoverageVerification